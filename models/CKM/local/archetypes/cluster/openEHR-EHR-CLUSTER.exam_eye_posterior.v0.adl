archetype (adl_version=1.4; uid=d1cd2ab5-197c-4b93-b615-b237371e0138)
	openEHR-EHR-CLUSTER.exam_eye_posterior.v0

concept
	[at0000]	-- Examination of posterior chamber
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2015-07-09">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording a narrative description and clinical interpretation of the findings observed during the physical examination of the posterior chamber of an eye.">
			use = <"Use to record a narrative description of the overall findings observed during the physical examination of the posterior chamber of an eye. This initial draft archetype consists only of the core examination pattern data elements (as per CLUSTER.exam_pattern) and will likely be expanded further over time as specific clinical requirements are identified.

This archetype has been specifically designed to be used in the 'Examination findings' SLOT within the CLUSTER.exam_eye archetype, but can also be used within the OBSERVATION.exam and other OBSERVATION or CLUSTER archetypes, where clinically appropriate.

Use to provide a framework in which CLUSTER archetypes can be nested in the 'Examination findings' SLOT to record additional structured physical examination findings. 

Identification of the device used to carry out the examination can be recorded in the Protocol of the OBSERVATION.exam archetype.

The CLUSTER.exclusion_exam archetype can be nested within the 'Examination not done' SLOT to optionally record explicit details about the examination not being performed.

Use to incorporate the narrative descriptions of clinical findings within existing or legacy clinical systems into an archetyped format, using the 'Clinical Description' data element.">
			keywords = <"eye", "posterior chamber">
			misuse = <"Not to be used to record findings of the physical examination of other parts of the eye - use specific purpose-built CLUSTER archetypes for this purpose. For example, CLUSTER.exam_eye_anterior.

Not to be used for recording the clinical history - use specific OBSERVATION and CLUSTER archetypes. For example OBSERVATION.story and CLUSTER.symptom.

Not to be used to record stand-alone clinical observations or measurements or test results - use specific OBSERVATION archetypes. For example OBSERVATION.blood_pressure or OBSERVATION.imaging_exam.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Silje Ljosland Bakke, Bergen Hospital Trust, Norway", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom", "John Tore Valand, Haukeland Universitetssjukehus, Norway", "Heather Leslie, Ocean Informatics, Australia">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["current_contact"] = <"Heather Leslie, Ocean Informatics<heather.leslie@oceaninformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"AF9365FA7386191D30D94A5E55794961">
		["build_uid"] = <"f983ffd3-22d6-4a3b-9407-14a7f83da410">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {	-- Examination of posterior chamber
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Eye examined
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0009, 	-- Left
							at0010]	-- Right
						}
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- No abnormality detected
				value matches {
					DV_BOOLEAN matches {
						value matches {True}
					}
				}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Clinical description
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0004] occurrences matches {0..*} matches {	-- Examination findings
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exam_eye_retina(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.exam_eye_vitreous(-[a-zA-Z0-9_]+)*\.v0/}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {	-- Multimedia representation
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0006] occurrences matches {0..*} matches {	-- Clinical interpretation
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0008] occurrences matches {0..*} matches {	-- Examination not done
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exclusion_exam(-[a-zA-Z0-9_]+)*\.v1/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Examination of posterior chamber">
					description = <"Findings observed during the physical examination of the posterior chamber of an eye.">
				>
				["at0001"] = <
					text = <"Eye examined">
					description = <"Identification of the eye under examination.">
					comment = <"If the side of eye being examined has previously been identified in another archetype in which this archetype is nested, this data element becomes redundant.">
				>
				["at0002"] = <
					text = <"No abnormality detected">
					description = <"Statement that no abnormality was detected (NAD) on physical examination.">
					comment = <"Record as True if no abnormality was detected on examination. Specific statements about the examination can be included in the 'Clinical Interpretation' data element.">
				>
				["at0003"] = <
					text = <"Clinical description">
					description = <"Narrative description of the overall findings observed during the physical examination.">
				>
				["at0004"] = <
					text = <"Examination findings">
					description = <"Structured details about the  physical examination findings.">
				>
				["at0005"] = <
					text = <"Multimedia representation">
					description = <"Digital image, video or diagram representing the physical examination findings.">
				>
				["at0006"] = <
					text = <"Clinical interpretation">
					description = <"Single word, phrase or brief description that represents the clinical meaning and significance of the physical examination findings.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the physical examination findings, not captured in other fields.">
				>
				["at0008"] = <
					text = <"Examination not done">
					description = <"Details to explicitly record that this examination was not performed.">
				>
				["at0009"] = <
					text = <"Left">
					description = <"The left eye was examined.">
				>
				["at0010"] = <
					text = <"Right">
					description = <"The right eye was examined.">
				>
			>
		>
	>
