archetype (adl_version=1.4; uid=d8ac401c-6267-4873-b56c-30ed5007c00c)
	openEHR-EHR-CLUSTER.medication_order_summary.v0

concept
	[at0000]	-- Medication course summary
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Ian McNicoll">
		["organisation"] = <"freshEHR Clinical Informatics Ltd.">
		["email"] = <"ian@freshehr.com">
		["date"] = <"2015-11-01">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To provide an overall summary of the status and key dates, related to a medication order.">
			use = <"Use within the context of a medication order Instruction where a summary of the overallcourse is required. This will normally be where the order is is being used within the context of a medicaton summary list, and not  in the context of an orderable prescription record, where medication ctions will normally carry the primary record of the status of the order and key date information.">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Medication item, Draft Archetype [Internet]. UK Clinical Models, UK Clinical Models Clinical Knowledge Manager [cited: 2015-11-01]. Available from: http://clinicalmodels.org.uk/ckm/#showArchetype_1051.32.3

Medication event summary, Draft Archetype [Internet]. UK Clinical Models, UK Clinical Models Clinical Knowledge Manager [cited: 2015-11-01]. Available from: http://clinicalmodels.org.uk/ckm/#showArchetype_1051.32.140

Medication order status valueset[Internet]. HL7 FHIR , HL7 FHIR DSTU2 [cited: 2015-11-01]. Available from https://www.hl7.org/fhir/valueset-medication-order-status.html">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"5F5752AC66418C0A633D91D05EE0B355">
		["build_uid"] = <"c2652081-bba2-4eab-95bc-17678d85e7d5">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {	-- Medication course summary
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Course status
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0021, 	-- Active
							at0022, 	-- Stopped
							at0023, 	-- Never active
							at0024, 	-- Completed
							at0025, 	-- Obsolete
							at0026, 	-- Suspended
							at0027]	-- Draft
						}
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..*} matches {	-- Key course dates
				name matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0003, 	-- Date ordered/recommended
							at0004, 	-- Date first prescription issued
							at0005, 	-- Date last prescription issued
							at0006, 	-- Date first authorised
							at0007, 	-- Date last authorised
							at0008, 	-- Date first dispensed
							at0009, 	-- Date last dispensed
							at0010, 	-- Date first administered
							at0011, 	-- Date last administered
							at0012, 	-- Date discontinued
							at0013, 	-- Date authorised
							at0014, 	-- Date prescription issued
							at0015, 	-- Date dispensed
							at0016, 	-- Date administered
							at0017, 	-- Date administration withheld
							at0018, 	-- Date reviewed
							at0019, 	-- Date last reviewed
							at0020]	-- Date changed
						}
					}
				}
				value matches {
					DV_DATE_TIME matches {*}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Medication course summary">
					description = <"Overall summary of the medication course.">
				>
				["at0001"] = <
					text = <"Course status">
					description = <"The overall status of this order.">
				>
				["at0002"] = <
					text = <"Key course dates">
					description = <"Key medication event dates.">
				>
				["at0003"] = <
					text = <"Date ordered/recommended">
					description = <"The data at which the medication course was first ordered or recommended.">
				>
				["at0004"] = <
					text = <"Date first prescription issued">
					description = <"The date at which the medication was first issued. 'Issued' refers to the prescription 'token' electronic or paper which authorises supply of a medication.">
				>
				["at0005"] = <
					text = <"Date last prescription issued">
					description = <"The date at which the medication prescription was last issued. This refers to the prescription 'token' electronic or paper which authorises supply of a medication.">
				>
				["at0006"] = <
					text = <"Date first authorised">
					description = <"The date at which the medication was first authorised.For a repeat prescription, authorisation refers to the creation of the repeat prescription 'master' which is followed by the production of one or more prescription issues.">
				>
				["at0007"] = <
					text = <"Date last authorised">
					description = <"The data at which the medication was last authorised.The date at which the medication was first authorised.For a repeat prescription, authorisation refers to the creation of the repeat prescription 'master' which is followed by the production of one or more prescription issues. Authorisation is generally only given for a limited period or limited number of issues, after which re-authorisation is required.">
				>
				["at0008"] = <
					text = <"Date first dispensed">
					description = <"The date at which the medicaton was first physically dispensed.">
				>
				["at0009"] = <
					text = <"Date last dispensed">
					description = <"The date at which the medication was last dispensed.">
				>
				["at0010"] = <
					text = <"Date first administered">
					description = <"The date at which the medication was first administered to the patient.">
				>
				["at0011"] = <
					text = <"Date last administered">
					description = <"The date at which the medication was last administered.">
				>
				["at0012"] = <
					text = <"Date discontinued">
					description = <"The date at which the medication was discontinued.">
				>
				["at0013"] = <
					text = <"Date authorised">
					description = <"The date at which the medication was authorised or re-authorised.">
				>
				["at0014"] = <
					text = <"Date prescription issued">
					description = <"The date at which a medication prescription was issued i.e the physical or electronic prescription token was created.">
				>
				["at0015"] = <
					text = <"Date dispensed">
					description = <"The date at which a medication was dispensed.">
				>
				["at0016"] = <
					text = <"Date administered">
					description = <"The date at which a medication was administered.">
				>
				["at0017"] = <
					text = <"Date administration withheld">
					description = <"The data at which administration of a medication was withheld or suspended.">
				>
				["at0018"] = <
					text = <"Date reviewed">
					description = <"The date at which the medication order was reviewed.">
				>
				["at0019"] = <
					text = <"Date last reviewed">
					description = <"The date at which the medication order was last reviewed.">
				>
				["at0020"] = <
					text = <"Date changed">
					description = <"The date at which the medication instruction was modified.">
				>
				["at0021"] = <
					text = <"Active">
					description = <"This is an active medication.">
				>
				["at0022"] = <
					text = <"Stopped">
					description = <"This is a medication that has previously been issued, dispensed or administered but has now been discontinued.">
				>
				["at0023"] = <
					text = <"Never active">
					description = <"A medication which was ordered or authorised but has been cancelled prior to being issued, dispensed or adiminstered.">
				>
				["at0024"] = <
					text = <"Completed">
					description = <"The medication course has been completed.">
				>
				["at0025"] = <
					text = <"Obsolete">
					description = <"This medication order has been superseded by another.">
				>
				["at0026"] = <
					text = <"Suspended">
					description = <"Actions reuulting from the order are to be temporarily halted, but are expected to continue later. May also be called 'on-hold'.">
				>
				["at0027"] = <
					text = <"Draft">
					description = <"The medication order has been made but further processes e.g. sign-off or verification are required before it becomes actionable.">
				>
			>
		>
	>
