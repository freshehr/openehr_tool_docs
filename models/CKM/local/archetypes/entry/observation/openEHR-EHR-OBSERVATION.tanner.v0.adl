archetype (adl_version=1.4; uid=ae257a35-ad5b-4749-b6ad-f8942bef4195)
	openEHR-EHR-OBSERVATION.tanner.v0

concept
	[at0000]	-- Tanner stages
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"John Tore Valand, Vebjørn Arntzen">
				["organisation"] = <"Helse Bergen HF, OUS HF">
			>
		>
	>
description
	original_author = <
		["name"] = <"Malin Aas Berg">
		["organisation"] = <"DIPS ASA">
		["email"] = <"mab@dips.no">
		["date"] = <"2017-03-17">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To describe progression of pubertal changes in genitals, breasts and pubic hair.">
			use = <"Normally used to describe progression of pubertal changes in children, but can also be used to adults, for example evaluate gynaecomasty in adult men.

Boys and girls are rated on a 5 point scale. Boys are rated for genital development and
pubic hair growth, and girls are rated for breast development and pubic hair growth.">
			keywords = <"pubertal", "puberty", "genitals", "breasts", "breast", "pubical", "pubic hair">
			misuse = <"">
			copyright = <"© Nasjonal IKT HF, openEHR Foundation">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å skåre utvikling av genitalia, bryst og pubesbehåring.">
			use = <"Brukes vanligvis for å beskrive et barns pubertetsutvikling, men kan også brukes hos voksne for eksempel for å vurdere grad av gynekomasti hos voksne menn.

Gutter og jenter vurderes ut fra en 5 poengs skala. Gutter vurderes ut fra utvikling av genitalia og pubesbehåring, jenter vurderes ut fra utvikling av bryst og pubesbehåring.">
			keywords = <"pubertet", "genitalia", "bryst", "bryster", "behåring", "pubes", "kjønnsmodning">
			misuse = <"">
			copyright = <"© Nasjonal IKT HF, openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Derived from: Tanner stadier, Draft archetype [Internet]. Nasjonal IKT, Nasjonal IKT Clinical Knowledge Manager [cited: 2017-05-05]. Available from: http://arketyper.no/ckm/#showArchetype_1078.36.1823
Marshall WA, Tanner JM. Variations in the Pattern of Pubertal Changes in Boys. Archives of Disease in Childhood. 1970 [cited 2017 Mar 17]; 45(239): 13-23. Available from https://www.ncbi.nlm.nih.gov/pmc/articles/PMC2020414/

Marshall WA, Tanner JM. Variations in pattern of pubertal changes in girls. Archives of Disease in Childhood. 1969 [cited 2017 Mar 17]; 44(235): 291-303. Available from https://www.ncbi.nlm.nih.gov/pmc/articles/PMC2020314/

Biro F M, Chan Y-M (Dec 14, 2016). Normal puberty. UpToDate [Internett]. 
Available at: https://www.uptodate.com/contents/normal-puberty?source=search_result&search=tanner&selectedTitle=1~66#H4 [Accessed 2017 Apr 25].

Norsk Elektronisk Legehåndbok (Nov 15.2015). Tanner Stadier. [Internett]. 
Available at: https://legehandboka.no/handboken/kliniske-kapitler/pediatri/pasientinformasjon/vekst-og-utvikling/pubertet-for-tidlig/ [Accessed 2017 Apr 25].">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"1D7FAF885D5BBC3943DFB1B60617693D">
		["build_uid"] = <"6173b9cf-12a3-4ca9-96bf-db7a3ed76567">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {	-- Tanner stadier
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..1} matches {	-- Uspesifisert hendelse
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- Genitalia
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0007, 	-- Tanner I
													at0008, 	-- Tanner II
													at0009, 	-- Tanner III
													at0010, 	-- Tanner IV
													at0011]	-- Tanner V
												}
											}
										}
									}
									ELEMENT[at0005] occurrences matches {0..1} matches {	-- Bryst
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0012, 	-- Tanner I
													at0013, 	-- Tanner II
													at0014, 	-- Tanner III
													at0015, 	-- Tanner IV
													at0016]	-- Tanner V
												}
											}
										}
									}
									ELEMENT[at0006] occurrences matches {0..1} matches {	-- Pubesbehåring
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0017, 	-- Tanner I
													at0018, 	-- Tanner II
													at0019, 	-- Tanner III
													at0020, 	-- Tanner IV
													at0021]	-- Tanner V
												}
											}
										}
									}
									ELEMENT[at0027] occurrences matches {0..1} matches {	-- Kommentar
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0022] matches {	-- *Tree(nb)
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0025] occurrences matches {0..*} matches {	-- Tilleggsinformasjon
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}



ontology
	term_definitions = <
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Tanner stadier">
					description = <"En skala for å beskrive et barns pubertetsutvikling.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Uspesifisert hendelse">
					description = <"Standard, uspesifisert tidspunkt eller tidsintervall som kan defineres mer eksplisitt i en template eller i en applikasjon.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Genitalia">
					description = <"Stadier for utvikling av mannlige genitalia.">
				>
				["at0005"] = <
					text = <"Bryst">
					description = <"Stadier for utvikling av bryst hos unge kvinner.">
				>
				["at0006"] = <
					text = <"Pubesbehåring">
					description = <"Stadier for utvikling av pubesbehåring hos kvinner og menn.">
				>
				["at0007"] = <
					text = <"G1">
					description = <"Testikler, skrotum og penis slik som i tidlig barndom.">
				>
				["at0008"] = <
					text = <"G2">
					description = <"Skrotum og testikler har vokst og teksturen av skrotumhud er grovere.">
				>
				["at0009"] = <
					text = <"G3">
					description = <"Penis har vokst, mest i lengden. Skrotum og testikler har vokst ytterligere.">
				>
				["at0010"] = <
					text = <"G4">
					description = <"Penis har vokst i lengde og tykkelse. Begynnende utvikling av glans. Testes og skrotum vokser videre og skrotal hud er mørkere.">
				>
				["at0011"] = <
					text = <"G5">
					description = <"Voksent utseende på genitalia.">
				>
				["at0012"] = <
					text = <"B1">
					description = <"Ingen synlig brystutvikling.">
				>
				["at0013"] = <
					text = <"B2">
					description = <"Begynnende brystutvikling, bryst og papille lett forhøyet, areolas diameter lett økt.">
				>
				["at0014"] = <
					text = <"B3">
					description = <"Videre vekst av brystet men fortsatt jevn kontur.">
				>
				["at0015"] = <
					text = <"B4">
					description = <"Areola og papille skilles ut som en egen kontur på brystet (hevet fra resten av brystkonturen).">
				>
				["at0016"] = <
					text = <"B5">
					description = <"Voksen. Kun papillen er hevet ut fra resten av bryskonturen.">
				>
				["at0017"] = <
					text = <"P1">
					description = <"Ingen pubesbehåring (på samme nivå som eventuell behåring på abdomen).">
				>
				["at0018"] = <
					text = <"P2">
					description = <"Kvinner: Begynnende vekst av lyse, tynne, rette hår langs labia. Kan være lett å overse. Menn: Sparsom vekst av lett pigmentert, rett, dunaktig hår. Hovedsakelig ved basis av penis. Kan være lett å overse.">
				>
				["at0019"] = <
					text = <"P3">
					description = <"Mørkere, tykkere og mer krøllete behåring. Sparsom utbredelse opp mot abdomen.">
				>
				["at0020"] = <
					text = <"P4">
					description = <"Voksen type behåring, men ubredelse er mindre. Brer seg ikke over til mediale del av låret.">
				>
				["at0021"] = <
					text = <"P5">
					description = <"Voksen mengde og utbredelse i form av invertert triangel og behåringen kan bre seg over til mediale del av låret.">
				>
				["at0022"] = <
					text = <"*Tree(nb)">
					description = <"*@ internal @(nb)">
				>
				["at0025"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR eller CIMI.">
				>
				["at0027"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om Tanner stadiene som ikke er registrert i andre felt.">
				>
			>
		>
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Tanner stages">
					description = <"A scale to describe the onset an progression of pubertal changes.">
				>
				["at0001"] = <
					text = <"*Event Series(nb)">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"*Tree(nb)">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Genitals">
					description = <"The stages for development in male genitals.">
				>
				["at0005"] = <
					text = <"Breasts">
					description = <"The stages for breast development in young women.">
				>
				["at0006"] = <
					text = <"Pubic hair">
					description = <"The stages for pubic hair development in females and males.">
				>
				["at0007"] = <
					text = <"G1">
					description = <"Prepubertal.">
				>
				["at0008"] = <
					text = <"G2">
					description = <"Enlargement of scrotum and testes; scrotal skin reddens and changes in texture.">
				>
				["at0009"] = <
					text = <"G3">
					description = <"Enlargement of penis (length at first); further growth of testes.">
				>
				["at0010"] = <
					text = <"G4">
					description = <"Increased size of penis with growth in breadth and development of glans; testes and scrotum larger, scrotal skin darker.">
				>
				["at0011"] = <
					text = <"G5">
					description = <"Adult genitalia.">
				>
				["at0012"] = <
					text = <"B1">
					description = <"Prepubertal.">
				>
				["at0013"] = <
					text = <"B2">
					description = <"Breast bud stage with elevation of breast and papilla; enlargement of areola.">
				>
				["at0014"] = <
					text = <"B3">
					description = <"Further enlargement of breast and areola; no separation of their contour.">
				>
				["at0015"] = <
					text = <"B4">
					description = <"Areola and papilla form a secondary mound above level of breast.">
				>
				["at0016"] = <
					text = <"B5">
					description = <"Mature stage: projection of papilla only, related to recession of areola.">
				>
				["at0017"] = <
					text = <"P1">
					description = <"Prepubertal (the pubic area may have vellus hair, similar to that of forearms).">
				>
				["at0018"] = <
					text = <"P2">
					description = <"Sparse growth of long, slightly pigmented hair, straight or curled, at base of penis or along labia.">
				>
				["at0019"] = <
					text = <"P3">
					description = <"Darker, coarser and more curled hair, spreading sparsely over junction of pubes.">
				>
				["at0020"] = <
					text = <"P4">
					description = <"Hair adult in type, but covering smaller area than in adult; no spread to medial surface of thighs.">
				>
				["at0021"] = <
					text = <"P5">
					description = <"Adult in type and quantity, with horizontal upper border.">
				>
				["at0022"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0025"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: Local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0027"] = <
					text = <"Comment">
					description = <"Additional narrative about the Tanner stages not captured in other fields.">
				>
			>
		>
	>
