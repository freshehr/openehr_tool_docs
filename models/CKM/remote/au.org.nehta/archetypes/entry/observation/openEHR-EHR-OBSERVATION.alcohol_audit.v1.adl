archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.alcohol_audit.v1

concept
	[at0000]	-- Alcohol Use Disorders Identification Test (AUDIT)
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2013-06-10">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To identify individuals with hazardous and harmful patterns of alcohol consumption, indicators of alcohol dependence and some consequences from harmful drinking.">
			use = <"Use to record the results of the AUDIT screening test (or the AUDIT-C subset) as a means to identify individuals with hazardous and harmful patterns of alcohol consumption, indicators of alcohol dependence and some consequences from harmful drinking. The test can be administered during an oral interview or as a self-reported questionnaire.

Each of the questions in the AUDIT test will be recorded as a separate data element. For pragmatic reasons, in this archetype, each data element is not labelled as the full question, but as a summary of the question topic. The full question to which each data element refers is identified in the 'Description' for each data element. For example: \"How often did you have six or more drinks on one occasion in the past year?\" is represented as the 'Bingeing' data element.

The AUDIT-C test is a shortened version of the full AUDIT test, using only the first three questions related to consumption.">
			keywords = <"AUDIT", "AUDIT-C", "alcohol", "binge">
			misuse = <"Not to be used to record a diary of alcohol consumption - use OBSERVATION.alcohol_use.

Not to be used to record a summary about the individual's alcohol consumption - use EVALUATION.alcohol_summary.">
			copyright = <"© National E-Health Transition Authority">
		>
	>
	lifecycle_state = <"CommitteeDraft">
	other_contributors = <>
	other_details = <
		["references"] = <"Babor T, Higgins-Biddle J, Saunders J, Monteiro M. The Alcohol Use Disorders Identification Test: Guidelines for Use in Primary Care, Second Edition [Internet]. Geneva: Department of Mental Health and Substance Dependence, World Health Organization. 2001 (Accessed 2013 Jun 10). Available at: http://whqlibdoc.who.int/hq/2001/WHO_MSD_MSB_01.6a.pdf.

Alcohol Screen (AUDIT). Australian Government, Department of Veteran's Affairs. (Accessed 2013 Jun 11). Available at: http://www.health.nt.gov.au/library/scripts/objectifyMedia.aspx?file=pdf/63/68.pdf

The AUDIT Alcohol Consumption Questions (Audit – C). Northern Territory Government, Department of Health & Families. (Accessed 2013 Jun 10). Available from: http://www.health.nt.gov.au/library/scripts/objectifyMedia.aspx?file=pdf/64/44.pdf.

Alcohol Use Disorders Identification Test. US Department of Venterans Affairs. (Accessed 2013 Jun 10). Available at: http://www.hepatitis.va.gov/provider/tools/audit-c.asp.">
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["MD5-CAM-1.0.1"] = <"D25E99B0839398E91B638E1AA66CC795">
	>

definition
	OBSERVATION[at0000] matches {	-- Alcohol Use Disorders Identification Test (AUDIT)
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					POINT_EVENT[at0002] occurrences matches {0..1} matches {	-- Specific Date/Time
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- Frequency of Consumption
										value matches {
											0|[local::at0005], 	-- Never
											1|[local::at0006], 	-- Monthly or Less
											2|[local::at0007], 	-- 2 to 4 Times a Month
											3|[local::at0008], 	-- 2 to 3 Times a Week
											4|[local::at0009]  	-- 4 or More Times a Week
										}
									}
									ELEMENT[at0010] occurrences matches {0..1} matches {	-- Typical Consumption
										value matches {
											0|[local::at0011], 	-- 1 or 2
											1|[local::at0012], 	-- 3 or 4
											2|[local::at0013], 	-- 5 or 6
											3|[local::at0014], 	-- 7 to 9
											4|[local::at0015]  	-- 10 or More
										}
									}
									ELEMENT[at0016] occurrences matches {0..1} matches {	-- Binge Drinking
										value matches {
											0|[local::at0017], 	-- Never
											1|[local::at0018], 	-- Less than Monthly
											2|[local::at0019], 	-- Monthly
											3|[local::at0020], 	-- Weekly
											4|[local::at0021]  	-- Daily or Almost Daily
										}
									}
									ELEMENT[at0023] occurrences matches {0..1} matches {	-- Inability to Stop
										value matches {
											0|[local::at0017], 	-- Never
											1|[local::at0018], 	-- Less than Monthly
											2|[local::at0019], 	-- Monthly
											3|[local::at0020], 	-- Weekly
											4|[local::at0021]  	-- Daily or Almost Daily
										}
									}
									ELEMENT[at0024] occurrences matches {0..1} matches {	-- Failed Expectations
										value matches {
											0|[local::at0017], 	-- Never
											1|[local::at0018], 	-- Less than Monthly
											2|[local::at0019], 	-- Monthly
											3|[local::at0020], 	-- Weekly
											4|[local::at0021]  	-- Daily or Almost Daily
										}
									}
									ELEMENT[at0025] occurrences matches {0..1} matches {	-- Morning Drinking
										value matches {
											0|[local::at0017], 	-- Never
											1|[local::at0018], 	-- Less than Monthly
											2|[local::at0019], 	-- Monthly
											3|[local::at0020], 	-- Weekly
											4|[local::at0021]  	-- Daily or Almost Daily
										}
									}
									ELEMENT[at0026] occurrences matches {0..1} matches {	-- Guilt/Remorse
										value matches {
											0|[local::at0017], 	-- Never
											1|[local::at0018], 	-- Less than Monthly
											2|[local::at0019], 	-- Monthly
											3|[local::at0020], 	-- Weekly
											4|[local::at0021]  	-- Daily or Almost Daily
										}
									}
									ELEMENT[at0027] occurrences matches {0..1} matches {	-- Loss of Memory
										value matches {
											0|[local::at0017], 	-- Never
											1|[local::at0018], 	-- Less than Monthly
											2|[local::at0019], 	-- Monthly
											3|[local::at0020], 	-- Weekly
											4|[local::at0021]  	-- Daily or Almost Daily
										}
									}
									ELEMENT[at0028] occurrences matches {0..1} matches {	-- Injuries
										value matches {
											0|[local::at0030], 	-- No
											2|[local::at0031], 	-- Yes, but not in the last year.
											4|[local::at0032]  	-- Yes, during the last year.
										}
									}
									ELEMENT[at0029] occurrences matches {0..1} matches {	-- External Concern
										value matches {
											0|[local::at0030], 	-- No
											2|[local::at0031], 	-- Yes, but not in the last year.
											4|[local::at0032]  	-- Yes, during the last year.
										}
									}
									ELEMENT[at0022] occurrences matches {0..1} matches {	-- AUDIT Total Score
										value matches {
											DV_COUNT matches {
												magnitude matches {|0..40|}
											}
										}
									}
									ELEMENT[at0033] occurrences matches {0..1} matches {	-- AUDIT-C Total Score
										value matches {
											DV_COUNT matches {
												magnitude matches {|0..12|}
											}
										}
									}
									ELEMENT[at0034] occurrences matches {0..1} matches {	-- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
	}



ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Alcohol Use Disorders Identification Test (AUDIT)">
					description = <"Ten question screening test to identify harmful alcohol consumption.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Specific Date/Time">
					description = <"The specified point-in-time of the assessment.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Frequency of Consumption">
					description = <"How often did you have a drink containing alcohol in the past year?">
				>
				["at0005"] = <
					text = <"Never">
					description = <"No drinking of alcohol in the past year.">
				>
				["at0006"] = <
					text = <"Monthly or Less">
					description = <"Drinking alcohol monthly or less frequently during the past year.">
				>
				["at0007"] = <
					text = <"2 to 4 Times a Month">
					description = <"Drinking alcohol two to four times a month during the past year.">
				>
				["at0008"] = <
					text = <"2 to 3 Times a Week">
					description = <"Drinking alcohol two to three times a week during the past year.">
				>
				["at0009"] = <
					text = <"4 or More Times a Week">
					description = <"Drinking alcohol four or more times a week during the past year.">
				>
				["at0010"] = <
					text = <"Typical Consumption">
					description = <"How many drinks did you have on a typical day when you were drinking in the past year?">
				>
				["at0011"] = <
					text = <"1 or 2">
					description = <"One or two drinks of alcohol on a typical day in the past year.">
				>
				["at0012"] = <
					text = <"3 or 4">
					description = <"Three or four drinks of alcohol on a typical day in the past year.">
				>
				["at0013"] = <
					text = <"5 or 6">
					description = <"Five or six drinks of alcohol on a typical day in the past year.">
				>
				["at0014"] = <
					text = <"7 to 9">
					description = <"Seven, eight or nine drinks of alcohol on a typical day in the past year.">
				>
				["at0015"] = <
					text = <"10 or More">
					description = <"Ten or more drinks of alcohol on a typical day in the past year.">
				>
				["at0016"] = <
					text = <"Binge Drinking">
					description = <"How often did you have six or more drinks on one occasion in the past year?">
				>
				["at0017"] = <
					text = <"Never">
					description = <"Never, in the past year.">
				>
				["at0018"] = <
					text = <"Less than Monthly">
					description = <"Less than monthly, during the past year.">
				>
				["at0019"] = <
					text = <"Monthly">
					description = <"Monthly, during the past year.">
				>
				["at0020"] = <
					text = <"Weekly">
					description = <"Weekly, during the past year.">
				>
				["at0021"] = <
					text = <"Daily or Almost Daily">
					description = <"Daily, or almost daily, during the past year.">
				>
				["at0022"] = <
					text = <"AUDIT Total Score">
					description = <"Total Score calculated from the 10 AUDIT questions.">
				>
				["at0023"] = <
					text = <"Inability to Stop">
					description = <"How often during the last year have you found that you were not able to stop drinking once you had started?">
				>
				["at0024"] = <
					text = <"Failed Expectations">
					description = <"How often during the last year have you failed to do what was normally expected of you because of drinking?">
				>
				["at0025"] = <
					text = <"Morning Drinking">
					description = <"How often during the last year have you needed a first drink in the morning to get yourself going after a heavy drinking session?">
				>
				["at0026"] = <
					text = <"Guilt/Remorse">
					description = <"How often during the last year have you had a feeling of guilt or remorse after drinking?">
				>
				["at0027"] = <
					text = <"Loss of Memory">
					description = <"How often during the last year have you been unable to remember what happened the night before because of your drinking?">
				>
				["at0028"] = <
					text = <"Injuries">
					description = <"Have you or someone else been injured because of your drinking?">
				>
				["at0029"] = <
					text = <"External Concern">
					description = <"Has a relative, friend, doctor, or other health care worker been concerned about your drinking or suggested you cut down?">
				>
				["at0030"] = <
					text = <"No">
					description = <"No occurrence.">
				>
				["at0031"] = <
					text = <"Yes, but not in the last year.">
					description = <"Yes, this has occurred, but not in the last year.">
				>
				["at0032"] = <
					text = <"Yes, during the last year.">
					description = <"Yes this has occurred during the last year.">
				>
				["at0033"] = <
					text = <"AUDIT-C Total Score">
					description = <"Total Score calculated from the first 3 questions only.">
				>
				["at0034"] = <
					text = <"Comment">
					description = <"Additional narrative about the screening test not captured in other fields.">
				>
			>
		>
	>
