archetype (adl_version=1.4)
	openEHR-EHR-CLUSTER.palpation_of_fetus.v1

concept
	[at0000]	-- Palpation of Fetus
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"2007-04-19">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Record clinical examination findings on palpation of the fetus in utero, either from an abdominal or vaginal approach.">
			use = <"Use to record examination findings on palpation of the fetus while in utero. This archetype can be used to record either abdominal or vaginal examination findings, including during labor.

This archetype has been designed to be nested inside the OBSERVATION.exam archetype or other relevant CLUSTER archetypes, for example providing specific detail about the fetus in CLUSTER.palpation_of_uterus.

Record the details of only one fetus using this archetype. Recording details about multiple fetuses will require the multiple separate instances of this archetype.">
			keywords = <"uterus", "fetus", "foetus">
			misuse = <"">
			copyright = <"© National E-Health Transition Authority">
		>
	>
	lifecycle_state = <"CommitteeDraft">
	other_contributors = <"Rita Apelt, Department of Health,NT, Australia", "Stephen Chu, NEHTA, Australia", "Tim Garden, NTG Department of Health, Australia", "Sam Heard, Ocean Informatics, Australia (Editor)", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Ian McNicoll, Ocean Informatics UK, United Kingdom", "Jeremy Oats, NT Health, Australia", "Jo Wright, NT Dept of Health, Australia (Editor)">
	other_details = <
		["references"] = <"openEHR Clinical Knowledge Manager [Internet]. London: openEHR Foundation. [Draft CLUSTER archetype] Examination of the fetus; [authored 2007 Apr 19, cited 2011 Aug 12]. Available from : http://www.openehr.org/knowledge/OKM.html#showArchetype_1013.1.277_4.">
		["MD5-CAM-1.0.1"] = <"DDE1874AB473F41AC1044000DACE48FD">
	>

definition
	CLUSTER[at0000] matches {	-- Palpation of Fetus
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Clinical Description
				value matches {
					DV_TEXT matches {*}
				}
			}
			CLUSTER[at0002] occurrences matches {0..1} matches {	-- Abdominal Findings
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0003] occurrences matches {0..1} matches {	-- Lie
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0004, 	-- Longitudinal
									at0006, 	-- Oblique
									at0005]	-- Transverse
								}
							}
						}
					}
					ELEMENT[at0007] occurrences matches {0..1} matches {	-- Abdominal Presentation
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0008, 	-- Vertex
									at0071, 	-- Brow
									at0072, 	-- Face
									at0009, 	-- Breech
									at0073]	-- Shoulder
								}
							}
						}
					}
					ELEMENT[at0011] occurrences matches {0..1} matches {	-- Position
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0015, 	-- Occipito-Anterior (OA)
									at0018, 	-- Left Occipito-Anterior (LOA)
									at0017, 	-- Left Occipito-Transverse (LOT)
									at0019, 	-- Left Occipito-Posterior (LOP)
									at0013, 	-- Right Occipito-Anterior (ROA)
									at0012, 	-- Right Occipito-Transverse (ROT)
									at0014, 	-- Right Occipito-Posterior (ROP)
									at0016, 	-- Occipito-Posterior (OP)
									at0023, 	-- Sacro-Anterior (SA)
									at0026, 	-- Left Sacro-Anterior (LSA)
									at0025, 	-- Left Sacro-transverse (LST)
									at0027, 	-- Left Sacro-Posterior (LSP)
									at0021, 	-- Right Sacro-Anterior (RSA)
									at0020, 	-- Right Sacro-Transverse (RST)
									at0022, 	-- Right Sacro-Posterior (RSP)
									at0024, 	-- Sacro-Posterior (SP)
									at0031, 	-- Mento-Anterior (MA)
									at0034, 	-- Left Mento-Anterior (LMA)
									at0033, 	-- Left Mento-Transverse (LMT)
									at0035, 	-- Left Mento-Posterior (LMP)
									at0029, 	-- Right Mento-Anterior (RMA)
									at0028, 	-- Right Mento-Transverse (RMT)
									at0030, 	-- Right Mento-Posterior (RMP)
									at0032]	-- Mento-Posterior (MP)
								}
							}
						}
					}
					ELEMENT[at0036] occurrences matches {0..1} matches {	-- Attitude
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0037, 	-- Flexed
									at0038, 	-- Deflexed
									at0039]	-- Extended
								}
							}
						}
					}
					ELEMENT[at0047] occurrences matches {0..1} matches {	-- Engagement description
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0048, 	-- High and mobile
									at0049, 	-- At pelvic brim
									at0050]	-- Engaged
								}
							}
						}
					}
					ELEMENT[at0040] occurrences matches {0..1} matches {	-- Engagement estimation
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0041, 	-- Not engaged (0/5)
									at0042, 	-- 1/5
									at0043, 	-- 2/5
									at0044, 	-- 3/5
									at0045, 	-- 4/5
									at0046]	-- Fully engaged (5/5)
								}
							}
						}
					}
					ELEMENT[at0051] occurrences matches {0..1} matches {	-- Relative size
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0052, 	-- Small for dates
									at0054, 	-- Normal
									at0056]	-- Large for dates
								}
							}
						}
					}
					ELEMENT[at0057] occurrences matches {0..1} matches {	-- Estimated weight
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::124]>
								list = <
									["1"] = <
										units = <"kg">
									>
									["2"] = <
										units = <"gm">
									>
								>
							>
						}
					}
				}
			}
			CLUSTER[at0058] occurrences matches {0..1} matches {	-- Vaginal Findings
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0059] occurrences matches {0..1} matches {	-- Vaginal Presentation
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0060, 	-- Vertex
									at0009, 	-- Breech
									at0061, 	-- Shoulder
									at0065, 	-- Face
									at0066, 	-- Brow
									at0062, 	-- Foot
									at0063]	-- Arm
								}
							}
						}
					}
					ELEMENT[at0064] occurrences matches {0..1} matches {	-- Cord presentation?
						value matches {
							DV_BOOLEAN matches {
								value matches {True, False}
							}
						}
					}
					use_node ELEMENT occurrences matches {0..1} /items[at0002]/items[at0011]	-- /items[Abdominal Findings]/items[Position]
					ELEMENT[at0067] occurrences matches {0..1} matches {	-- Station
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::122]>
								list = <
									["1"] = <
										units = <"cm">
										magnitude = <|-5.0..5.0|>
									>
								>
							>
						}
					}
					use_node ELEMENT occurrences matches {0..1} /items[at0002]/items[at0036]	-- /items[Abdominal Findings]/items[Attitude]
					ELEMENT[at0068] occurrences matches {0..1} matches {	-- Caput
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0069] occurrences matches {0..1} matches {	-- Moulding
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0070] occurrences matches {0..1} matches {	-- Asynclitism
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
	}



ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Palpation of Fetus">
					description = <"Examination of the fetus by palpation, using either an abdominal or vaginal approach.">
				>
				["at0001"] = <
					text = <"Clinical Description">
					description = <"Narrative description of findings on examination of the fetus.">
				>
				["at0002"] = <
					text = <"Abdominal Findings">
					description = <"Clinical findings on abdominal palpation.">
				>
				["at0003"] = <
					text = <"Lie">
					description = <"Relationship of the longitudinal axis of the fetus to the long axis of the mother.">
				>
				["at0004"] = <
					text = <"Longitudinal">
					description = <"The fetal longitudinal axis is in alignment with the mother's longitudinal axis.">
				>
				["at0005"] = <
					text = <"Transverse">
					description = <"The fetal longitudinal axis is at right angles to the mother's longitudinal axis.">
				>
				["at0006"] = <
					text = <"Oblique">
					description = <"The fetal longitudinal axis is at an oblique angle to the mother's longitudinal axis. Sometimes also referred to as 'Unstable Lie'.">
				>
				["at0007"] = <
					text = <"Abdominal Presentation">
					description = <"Identification of the part of the fetus lying in the lower part of the uterus.">
				>
				["at0008"] = <
					text = <"Vertex">
					description = <"The vertex is lying in the lower part of the uterus.">
				>
				["at0009"] = <
					text = <"Breech">
					description = <"The breech is lying in the lower part of the uterus.">
				>
				["at0011"] = <
					text = <"Position">
					description = <"Relationship between the fetal denominator and the maternal pelvis.">
					comment = <"For cephalic presentations, the denominator is the occiput; in breech presentations the denominator is the sacrum; and in face presentations the denominator is the mentum.">
				>
				["at0012"] = <
					text = <"Right Occipito-Transverse (ROT)">
					description = <"The fetal occiput is pointing towards the right. Also known as Right Occipito-Lateral (ROL).">
				>
				["at0013"] = <
					text = <"Right Occipito-Anterior (ROA)">
					description = <"The fetal occiput is pointing anteriorly and towards the right.">
				>
				["at0014"] = <
					text = <"Right Occipito-Posterior (ROP)">
					description = <"The fetal occiput is pointing posteriorly and towards the right.">
				>
				["at0015"] = <
					text = <"Occipito-Anterior (OA)">
					description = <"The fetal occiput is pointing anteriorly, towards the pubic symphysis.">
				>
				["at0016"] = <
					text = <"Occipito-Posterior (OP)">
					description = <"The fetal occiput is pointing posteriorly, towards the sacrum.">
				>
				["at0017"] = <
					text = <"Left Occipito-Transverse (LOT)">
					description = <"The fetal occiput is pointing towards the left. Also known as Left Occcipito-Lateral (LOL).">
				>
				["at0018"] = <
					text = <"Left Occipito-Anterior (LOA)">
					description = <"The fetal occiput is pointing anteriorly and towards the left.">
				>
				["at0019"] = <
					text = <"Left Occipito-Posterior (LOP)">
					description = <"The fetal occiput is pointing posteriorly and towards the left.">
				>
				["at0020"] = <
					text = <"Right Sacro-Transverse (RST)">
					description = <"The fetal sacrum is pointing towards the right.">
				>
				["at0021"] = <
					text = <"Right Sacro-Anterior (RSA)">
					description = <"The fetal sacrum is pointing anteriorly and towards the right.">
				>
				["at0022"] = <
					text = <"Right Sacro-Posterior (RSP)">
					description = <"The fetal sacrum is pointing posteriorly and towards the right.">
				>
				["at0023"] = <
					text = <"Sacro-Anterior (SA)">
					description = <"The fetal sacrum is pointing anteriorly, towards the pubic symphysis.">
				>
				["at0024"] = <
					text = <"Sacro-Posterior (SP)">
					description = <"The fetal sacrum is pointing posteriorly, towards the sacrum.">
				>
				["at0025"] = <
					text = <"Left Sacro-transverse (LST)">
					description = <"The fetal sacrum is pointing towards the left.">
				>
				["at0026"] = <
					text = <"Left Sacro-Anterior (LSA)">
					description = <"The fetal sacrum is pointing anteriorly and towards the left.">
				>
				["at0027"] = <
					text = <"Left Sacro-Posterior (LSP)">
					description = <"The fetal sacrum is pointing posteriorly and towards the left.">
				>
				["at0028"] = <
					text = <"Right Mento-Transverse (RMT)">
					description = <"The fetal chin is pointing towards the right.">
				>
				["at0029"] = <
					text = <"Right Mento-Anterior (RMA)">
					description = <"The fetal chin is pointing anteriorly and towards the right.">
				>
				["at0030"] = <
					text = <"Right Mento-Posterior (RMP)">
					description = <"The fetal chin is pointing posteriorly and towards the right.">
				>
				["at0031"] = <
					text = <"Mento-Anterior (MA)">
					description = <"The fetal chin is pointing anteriorly, towards the pubic symphysis.">
				>
				["at0032"] = <
					text = <"Mento-Posterior (MP)">
					description = <"The fetal chin is pointing posteriorly, towards the sacrum.">
				>
				["at0033"] = <
					text = <"Left Mento-Transverse (LMT)">
					description = <"The fetal chin is pointing towards the left.">
				>
				["at0034"] = <
					text = <"Left Mento-Anterior (LMA)">
					description = <"The fetal chin is pointing anteriorly and towards the left.">
				>
				["at0035"] = <
					text = <"Left Mento-Posterior (LMP)">
					description = <"The fetal chin is pointing posteriorly and towards the left.">
				>
				["at0036"] = <
					text = <"Attitude">
					description = <"Description of the relationship of the fetal head and limbs to the body of the fetus on palpation.">
				>
				["at0037"] = <
					text = <"Flexed">
					description = <"The fetus is fully flexed.">
				>
				["at0038"] = <
					text = <"Deflexed">
					description = <"The fetus is not flexed.">
				>
				["at0039"] = <
					text = <"Extended">
					description = <"The fetus is extended.">
				>
				["at0040"] = <
					text = <"Engagement estimation">
					description = <"The estimated proportion of the presenting part which is felt above the pelvic brim.">
				>
				["at0041"] = <
					text = <"Not engaged (0/5)">
					description = <"The head is not engaged in the pelvis.">
				>
				["at0042"] = <
					text = <"1/5">
					description = <"About 20% of the head is inside the pelvis.">
				>
				["at0043"] = <
					text = <"2/5">
					description = <"About 40% of the head is inside the pelvis.">
				>
				["at0044"] = <
					text = <"3/5">
					description = <"About 60% of the head is inside the pelvis.">
				>
				["at0045"] = <
					text = <"4/5">
					description = <"About 80% of the head is inside the pelvis.">
				>
				["at0046"] = <
					text = <"Fully engaged (5/5)">
					description = <"The head is inside the pelvis.">
				>
				["at0047"] = <
					text = <"Engagement description">
					description = <"Description of engagement of fetal head.">
				>
				["at0048"] = <
					text = <"High and mobile">
					description = <"The presenting part is high and mobile.">
				>
				["at0049"] = <
					text = <"At pelvic brim">
					description = <"The presenting part is at the pelvic brim.">
				>
				["at0050"] = <
					text = <"Engaged">
					description = <"The presenting part is engaged in the pelvis.">
				>
				["at0051"] = <
					text = <"Relative size">
					description = <"The relative size of the baby compared to the normal for the estimated gestation.">
				>
				["at0052"] = <
					text = <"Small for dates">
					description = <"The size of the fetus appears smaller than expected for estimaged gestation.">
				>
				["at0054"] = <
					text = <"Normal">
					description = <"The size of the fetus appears as expected for the estimated gestation.">
				>
				["at0056"] = <
					text = <"Large for dates">
					description = <"The size of the fetus appears larger than expected for the estimated gestation.">
				>
				["at0057"] = <
					text = <"Estimated weight">
					description = <"The estimated weight of the fetus on palpation.">
				>
				["at0058"] = <
					text = <"Vaginal Findings">
					description = <"Clinical findings on vaginal palpation.">
				>
				["at0059"] = <
					text = <"Vaginal Presentation">
					description = <"Identification of the presenting part of the fetus.">
				>
				["at0060"] = <
					text = <"Vertex">
					description = <"The vertex is the presenting part.">
				>
				["at0061"] = <
					text = <"Shoulder">
					description = <"A fetal shoulder is the presenting part.">
				>
				["at0062"] = <
					text = <"Foot">
					description = <"A fetal foot is the presenting part.">
				>
				["at0063"] = <
					text = <"Arm">
					description = <"A fetal arm is the presenting part.">
				>
				["at0064"] = <
					text = <"Cord presentation?">
					description = <"Is the umbilical cord presenting?">
					comment = <"Record this separately as it is usually presenting with another body part eg breech or shoulder.">
				>
				["at0065"] = <
					text = <"Face">
					description = <"The face is the presenting part.">
				>
				["at0066"] = <
					text = <"Brow">
					description = <"The brow is the presenting part.">
				>
				["at0067"] = <
					text = <"Station">
					description = <"Position of the presenting part relative to teh ischial spines.">
					comment = <"Record -5 to -1cm for when the presenting part is above the ischial spines; record 0 at the ischial spines; and record 1 to 5 when the presenting part is below the ischial spines.">
				>
				["at0068"] = <
					text = <"Caput">
					description = <"Description of caput.">
				>
				["at0069"] = <
					text = <"Moulding">
					description = <"Description of moulding.">
				>
				["at0070"] = <
					text = <"Asynclitism">
					description = <"Description of asynclitism.">
				>
				["at0071"] = <
					text = <"Brow">
					description = <"The brow is lying in the lower part of the uterus.">
				>
				["at0072"] = <
					text = <"Face">
					description = <"The face is lying in the lower part of the uterus.">
				>
				["at0073"] = <
					text = <"Shoulder">
					description = <"The shoulder is lying in the lower part of the uterus.">
				>
			>
		>
	>
